 R←Test_0010(stopFlag batchFlag);filename;myIni;data;data2;∆Match;thisSection;allValues;allValueNames;thisName;where;val1;val2;⎕TRAP
⍝ Check the "Convert" with and without "flat".
 R←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 filename←'TestCases/sample_001.ini'
 myIni←⎕NEW ##.IniFiles(,⊂filename)
 data←myIni.Get ⍬ ⍬
 data2←myIni.Convert ⎕NS''
 →FailsIf~0∊⍴data2.⎕NL-2
 ∆Match←{(⍺∊⍵)∧(⍵∊⍺)}
 →FailsIf~∧/(data2.⎕NL-9)∆Match(data[;0]~⊂'')

 :For thisSection :In data2.⎕NL-9
     allValueNames←data2.{⍵.⎕NL-2}data2.⍎thisSection
     →FailsIf~∧/allValueNames ∆Match(myIni.Get thisSection ⍬)[;0]
     :For thisName :In allValueNames
         val1←⊃myIni.Get thisSection thisName
         where←(data2.{⍵.⎕NL-2}data2.⍎thisSection)⍳⊂thisName
         val2←where data2.{⍺⊃⍵.⍎¨⍵.⎕NL-2}data2.⍎thisSection
         →FailsIf val1≢val2
     :EndFor
 :EndFor

 data←(0<⊃∘⍴¨data[;1])⌿data
 data2←'flat'myIni.Convert ⎕NS''
 →FailsIf 0∊∧/(data2.⎕NL-2)∊data[;1]
 →FailsIf 0∊∧/(data[;1])∊(data2.⎕NL-2)
 →FailsIf 0∊∧/(data2.(⍎¨⎕NL-2))∊data[;2]
 →FailsIf 0∊∧/(data[;2])∊data2.(⍎¨⎕NL-2)
 →FailsIf 12 2≢⍴data2.List

 R←∆OK
