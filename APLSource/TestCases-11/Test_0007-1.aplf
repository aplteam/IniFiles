 R←Test_0007(stopFlag batchFlag);buffer;filename;myIni;i;where;filename2;make;myIni2;⎕TRAP
⍝ Check of replacement syntax. Uses backslahs chars!
 R←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 filename←'TestCases/sample_001.ini'

 myIni←⎕NEW ##.IniFiles(,⊂filename)
 filename2←'TestCases/sample_001_new.ini'
 myIni[⊂'Paths:Workdir']←⊂'C:/Firlefanz'               ⍝ That overwrite's {} syntax deliberately
 myIni[⊂'Paths:Logdir']←⊂'{Home}\MoreLogFiles'         ⍝ That uses {} syntax deliberately
 myIni[⊂'Data:Nested']←⊂'{Home}\LogFiles' 'X:\elwehere\Certificates' 'D:\RootCertificates'  ⍝ That overwrite's and AND uses {} syntax while assigning a nested structure
   ⍝ ↓↓ That overwrite's and AND uses {} syntax AND changes data types while assigning a nested structure
 myIni[⊂'Data:Nested2']←⊂'{LastName} David''s pub' '{LastName} Max'' tombstone' '{Lastname} replacement' 999
   ⍝ ↓↓ Add a new nested structure! This will again take the local vars "Home"!!!!
 myIni[⊂'Data:Nested3']←⊂'{Home}\first' '{Home}\Second' 'F:\whatsoever' 888
 myIni.Save filename2

 myIni2←⎕NEW ##.IniFiles(,⊂filename2)
 →FailsIf myIni2[⊂'GenerAl:AppName']≢myIni2.Get'GenerAl' 'APpName'
 →FailsIf myIni2[⊂'General:VAt']≢myIni2.Get'General' 'vat'
 →FailsIf myIni2[⊂'PathS:Home']≢myIni2.Get'Paths' 'HOME'
 →FailsIf myIni2[⊂'Paths:WorkDir']≢myIni2.Get'Paths' 'WorkDir'
 →FailsIf myIni2[⊂'Paths:LogDir']≢myIni2.Get'Paths' 'LogDir'
 →FailsIf myIni2[⊂'Paths:TempDir']≢myIni2.Get'Paths' 'TempDir'
 →FailsIf(⊃myIni2[⊂'Paths:'])≢{⍵[;1+⎕IO]}myIni2.Get'Paths'⍬
 →FailsIf myIni2[⊂'Data:Nested']≢myIni2.Get'Data' 'Nested'
 →FailsIf myIni2[⊂'Data:Nested2']≢myIni2.Get'Data' 'Nested2'
 →FailsIf myIni2[⊂'Data:Lastname']≢myIni2.Get'Data' 'Lastname'
 →FailsIf myIni2[⊂'Data:Nested3']≢myIni2.Get'Data' 'Nested3'
 →FailsIf(⊃myIni2[⊂'Data:'])≢{⍵[;1+⎕IO]}myIni2.Get'Data'⍬

 →FailsIf(⊂'C:/Firlefanz')≢myIni2.Get'Paths:WorkDir'
 →FailsIf(⊂'C:\MyApp\MoreLogFiles')≢myIni2.Get'Paths:LogDir'
 buffer←('Jaeger''s David''s pub' 'Jaeger''s Max'' tombstone' 'Jaeger''s replacement' 999)
 →FailsIf~(⊃myIni2[⊂'Data:Nested2'])∆Match buffer
 buffer←'C:\AnotherApp\first' 'C:\AnotherApp\Second' 'F:\whatsoever' 888
 →FailsIf~(⊃myIni2[⊂'Data:Nested3'])∆Match buffer

 R←∆OK
